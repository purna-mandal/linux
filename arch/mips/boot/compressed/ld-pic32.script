/*
 * ld.script for compressed kernel support of MIPS
*
 * Copyright (C) 2014 Microchip Technology Inc.  All rights reserved.
 * Author: Joshua Henderson <joshua.henderson@microchip.com>
 *
 * This program is free software; you can redistribute	it and/or modify it
 * under  the terms of	the GNU General	 Public License as published by the
 * Free Software Foundation;  either version 2 of the  License, or (at your
 * option) any later version.
 */

OUTPUT_ARCH(mips)
ENTRY(start)

SECTIONS
{
	/* Text and read-only data */
	/* . = VMLINUZ_LOAD_ADDRESS; */

	.text : {
		*(.text)

		. = ALIGN(4);
		/* Put the compressed image here */
		__image_begin = .;
		*(.image)
		__image_end = .;
	}

	.rodata : {
		. = ALIGN(4);
		*(.rodata);
	}

	. = ALIGN(16);

	_etext = .; /* End of text section */
	__data_loc = .;      /* location in binary */

	. = 0xffffffff80000000;

	/* Writable data */
	.data : AT(__data_loc) {
		_sdata = .;
		*(.data)
		CONSTRUCTORS
		_fdata =  .;
	}
	_edata = .;
	/* End of data section */

	/* BSS */
	.bss : {
		*(.bss)
	}

	_end = .;

	/* Sections to be discarded */
	/DISCARD/ : {
		*(.MIPS.options)
		*(.options)
		*(.pdr)
		*(.reginfo)
		*(.comment)
		*(.note)
	}
}
